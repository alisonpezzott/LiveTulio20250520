## Roteiro  

### 1Âª parte: Power BI

- Demonstrar o Repo do GitHub;  
- Mostrar as opÃ§Ãµes do Power BI Desktop on/off; 
-  
- Ingerir os [dados](./dados) com Power BI Desktop;  
  - https://raw.githubusercontent.com/alisonpezzott/preco-versus-ipca/refs/heads/main/dados/item.csv  
  - https://raw.githubusercontent.com/alisonpezzott/preco-versus-ipca/refs/heads/main/dados/compras.csv  

- Ingerir tambÃ©m a query IPCA:  

```
let
    DataInicial = Date.ToText(#date(2021, 1, 1), "dd/MM/yyyy"),
    DataFinal = Date.ToText(Date.From(DateTime.LocalNow()), "dd/MM/yyyy"),

    Url = "https://api.bcb.gov.br",
    RelativePath = "dados/serie/bcdata.sgs.433/dados", 
    
    Query = [
        formato = "json",
        dataInicial = DataInicial,
        dataFinal = DataFinal
    ],

    Request = Web.Contents(
        Url,
        [ 
            RelativePath = RelativePath,
            Query = Query 
        ]
    ),

    Response = Json.Document(Request),
    
    Tabela = Table.FromRows(
        List.Transform(Response, Record.FieldValues), 
        {"Data", "VarMensal"}
    ),
    
    Adequacao = Table.TransformColumns(
        Tabela,
        {
            {"Data", Date.From, type date}, 
            {"VarMensal", each Number.FromText(_,"en-US") * 0.01, type number} 
        }
    )

in
    Adequacao
```

- Trazer a tabela [Calendario](https://github.com/alisonpezzott/calendario/blob/main/TMDLDesktop/TMDLDesktopPortuguese.tmdl);  
- Aplicar o tema;
- Fazer os relacionamentos;
- Criar a tabela de medidas;
- Desenvolver o problema e seguindo a lÃ³gica abaixo:  
  - Colocar um visual de tabela no canvas;
  - Incluir segmentadores de data e itens;  
  - Colocar a data e o item na tabela;  

#### CriaÃ§Ã£o das medidas   

```dax
Preco Unitario = AVERAGE('Compras'[PrecoUnit])
```

Encontrar a data anterior a Ãºltima venda.  

```
Data Anterior = 

VAR __CompraAnterior = 
    OFFSET(
        -1,
        SUMMARIZE(
            ALL('Compras'), 
            'Calendario'[Data],
            'Item'[Item]
        ),
        ORDERBY('Calendario'[Data], ASC),
        PARTITIONBY('Item'[Item])
    )

VAR __Data = 
    MAXX(__CompraAnterior, [Data])

RETURN
    IF(
        ISINSCOPE('Calendario'[Data]) 
            && ISINSCOPE('Item'[Item])
            && NOT ISBLANK([Preco Unitario]),
        __Data
    ) 

```   

Explicar a funÃ§Ã£o OFFSET e PARTITIONBY.

Agora, que a data da Ãºltima compra foi encontrada vamos trazer o preÃ§o da Ãºltima compra.  

```
Preco Anterior = 

VAR __DataAnterior = [Data Anterior]

RETURN

    CALCULATE(
        [Preco Unitario],
        'Calendario'[Data] = __DataAnterior
    ) 

```


Vamos agora encontrar a variaÃ§Ã£o percentual entre o preÃ§Ãµ atual e o anterior.  

```
Preco Var = 
VAR __PrecoAnterior =  [Preco Anterior]

RETURN
    DIVIDE([Preco Unitario]-__PrecoAnterior, __PrecoAnterior)

```  

Feito isso, aoga vamos encontrar a variaÃ§Ã£o do IPCA acumulado dinÃ¢mico.  

```
IPCA Acumulado = 

VAR __Window = 
    WINDOW(
        1, ABS,
        0, REL,
        ALLSELECTED('IPCA'[Data])
    )

RETURN
    CALCULATE(
        PRODUCTX(
            'IPCA',
            1 + 'IPCA'[VarMensal]
        ),
        __Window
    ) - 1

```

Focar na explicaÃ§Ã£o na funÃ§Ã£o Window.  

Como as datas do IPCA sÃ£o apenas inÃ­cios do mÃªs entÃ£o temos que encontrar as datas iniciais e finais do perÃ­odo.  

```
Data IPCA Ini = 
VAR __DataRef = [Data Anterior]
RETURN
    IF(
        __DataRef,
        EOMONTH(__DataRef, -1) + 1
    ) 
```

```
Data IPCA Fim = 
    IF(
        [Data Anterior],
        EOMONTH(SELECTEDVALUE('Compras'[DataPedido]), -1) + 1
    ) 

```  

Tendo as datas em mÃ£os nÃ³s conseguimos calcular a variaÃ§Ã£o do IPCA no perÃ­odo.  

```
IPCA Var = 
    IF(
        [Data Anterior],
        CALCULATE(
            [IPCA Acumulado],
            DATESBETWEEN('Calendario'[Data], [Data IPCA Ini], [Data IPCA Fim]),
            ALL('Calendario'[Data]) 
        )
    ) 
```  

Com isso agora conseguimos comparar a diferenÃ§a entre as variaÃ§Ãµes do IPCA e do PreÃ§o  

```
Preco - IPCA = [Preco Var] - [IPCA Var]  
```  

Vamos entÃ£o criar um flag que demonstrarÃ¡ a variaÃ§Ã£o ou se Ã© a primeira compra.  

```
Flag Var =
			
    VAR __DifPrecoVar = [Preco - IPCA]

    VAR __DataAnterior = [Data Anterior]

    RETURN

        SWITCH(
            TRUE(),
            NOT ISBLANK(__DataAnterior) && __DifPrecoVar > 0,  "ðŸ¡¥ acima IPCA",
            NOT ISBLANK(__DataAnterior) && __DifPrecoVar <= 0, "ðŸ¡§ abaixo IPCA",
            NOT ISBLANK([Preco Unitario]) && ISBLANK(__DataAnterior), "Primeira compra"
        )
```  

Agora queremos criar um segmentador de dados para filtrar as linhas de acordo com cada Flag.  Para isso vamos criar uma nova tabela chamada Flag

```Flag =  
    DATATABLE(
        "Flag", STRING, "FlagID", INTEGER,
        {
            {"ðŸ¡¥ acima IPCA", 0},
            {"ðŸ¡§ abaixo IPCA", 1},
            {"Primeira compra", 2}

        }
    )
```  

Com a tabela criada podemos incluir um segmentador de dados na tela.
E com este filtro vamos utilizar uma media no filtro lateral.  

```
Flag Filtro = 
IF( [Flag Var] IN VALUES('Flag'[Flag]), 1 )
```  

e para finalizar vamos criar duas medidas para formataÃ§Ã£o condicional da tabela.  


```
Flag Var Fundo =
			
    SWITCH(
        [Flag Var], 
        "ðŸ¡¥ acima IPCA", "#ffb7b7", 
        "ðŸ¡§ abaixo IPCA", "#d5f2cd",
        "Primeira compra", "#aed2e7"
    )

```  

```
Flag Var Fonte =

    SWITCH(
        [Flag Var], 
        "ðŸ¡¥ acima IPCA", "#802626", 
        "ðŸ¡§ abaixo IPCA", "#4b7041",
        "Primeira compra", "#1b4862"
    )
```  

Lembrar de organizar em pastas.
Demonstrar o GitHub Copilot para criar descriÃ§Ãµes de medidas.  

Salvar como arquivo PBIX PrecoVersusIPCA.pbix e publicar.  


### 2Âª parte: Demonstrar integraÃ§Ã£o com Git  
Salvar como arquivo PBIP. Abrir a pasta e excluir o arquivo PBIP fora da pasta.  
Fazer o teste de BPA e deploy.  


### 3Âª parte: Fabric  

Utilizar os notebooks e lakehouse 
Demonstrar o modelo semÃ¢ntico em Direct Lake
Integrar com git  

